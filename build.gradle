plugins {
    id "java-library"
}

version = "1.1.0"

repositories {
    mavenCentral()
}

compileJava {
    targetCompatibility "21"
    sourceCompatibility "21"
    options.compilerArgs << "-Xlint:deprecation"
}

configurations {
    mockitoAgent
}

dependencies {
    compileOnly libs.net.portswigger.burp.extensions.montoya.api

    implementation(libs.com.google.code.gson.gson)

    testImplementation libs.net.portswigger.burp.extensions.montoya.api
    testImplementation libs.org.hamcrest.hamcrest.all
    testImplementation libs.org.junit.jupiter
    testImplementation libs.org.slf4j.slf4j.simple
    testImplementation libs.org.mockito.mockito.core

    mockitoAgent(libs.org.mockito.mockito.core) { transitive = false }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    new File(projectDir, "src/main/resources/version.txt").text = """
            Version         : ${version}
            Build-By        : Gradle ${gradle.gradleVersion}
            Build-Timestamp : ${java.time.format.DateTimeFormatter.ISO_LOCAL_DATE_TIME.format(LocalDateTime.now())}
            Build-Revision  : ${"git rev-parse HEAD".execute().text.trim()}
            Build-Jdk       : ${System.properties["java.version"]}
            """.stripIndent().strip()
}

test {
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
    useJUnitPlatform()
}